///////////////////////////////////////////////////////////////////////////////
// NABLA - a Numerical Analysis Based LAnguage                               //
//                                                                           //
// Copyright (C) 2014~2015 CEA/DAM/DIF                                       //
// IDDN.FR.001.520002.000.S.P.2014.000.10500                                 //
//                                                                           //
// Contributor(s): CAMIER Jean-Sylvain - Jean-Sylvain.Camier@cea.fr          //
//                                                                           //
// This software is a computer program whose purpose is to translate         //
// numerical-analysis specific sources and to generate optimized code        //
// for different targets and architectures.                                  //
//                                                                           //
// This software is governed by the CeCILL license under French law and      //
// abiding by the rules of distribution of free software. You can  use,      //
// modify and/or redistribute the software under the terms of the CeCILL     //
// license as circulated by CEA, CNRS and INRIA at the following URL:        //
// "http://www.cecill.info".                                                 //
//                                                                           //
// The CeCILL is a free software license, explicitly compatible with         //
// the GNU GPL.                                                              //
//                                                                           //
// As a counterpart to the access to the source code and rights to copy,     //
// modify and redistribute granted by the license, users are provided only   //
// with a limited warranty and the software's author, the holder of the      //
// economic rights, and the successive licensors have only limited liability.//
//                                                                           //
// In this respect, the user's attention is drawn to the risks associated    //
// with loading, using, modifying and/or developing or reproducing the       //
// software by the user in light of its specific status of free software,    //
// that may mean that it is complicated to manipulate, and that also         //
// therefore means that it is reserved for developers and experienced        //
// professionals having in-depth computer knowledge. Users are therefore     //
// encouraged to load and test the software's suitability as regards their   //
// requirements in conditions enabling the security of their systems and/or  //
// data to be ensured and, more generally, to use and operate it in the      //
// same conditions as regards security.                                      //
//                                                                           //
// The fact that you are presently reading this means that you have had      //
// knowledge of the CeCILL license and that you accept its terms.            //
//                                                                           //
// See the LICENSE file for details.                                         //
///////////////////////////////////////////////////////////////////////////////


// **********************************************************
// * Conditions aux limites dans le cas 2D Triangles ou Quads
// **********************************************************
∀ outer faces @ 13.1 if (option_triangles || option_quads || option_hexa){
  const Real Y_MIN = option_y_min;
  const Real Y_MAX = option_y_max;
  const Real3x3 I = matrix3x3Id();
  const Real3 normaleY={0.0,1.0,0.0};
  ∀ node{
    if ((coord.y==Y_MIN)||(coord.y==Y_MAX)){
      const Real orientation = (coord.y==Y_MIN)?-1.0:1.0;
      const Real3 normal = orientation*normaleY;
      const Real3x3 nxn = normal⊗normal;
      const Real3x3 P = I-nxn;
      node_u_matrix = node_u_matrix ⊛ P; 
      node_u_matrix = P ⊛ node_u_matrix;
      node_u_matrix += nxn*(node_u_matrix.x.x +
                            node_u_matrix.y.y +
                            node_u_matrix.z.z );
      node_u_second_member = P ⨂ node_u_second_member;
    }
  }
}

∀ outer faces @ 13.3 if (option_triangles || option_quads || option_hexa){
  const Real X_MIN = option_x_min;
  const Real X_MAX = option_x_max;
  const Real value = option_u_ini;
  const Real3x3 I = matrix3x3Id();
  if (!(((coord[1].x==X_MIN)∧(coord[0].x==X_MIN)) ||
        ((coord[1].x==X_MAX)∧(coord[0].x==X_MAX)))) continue;
  ∀ node{
    node_u_matrix = I;
    node_u_second_member = value;
  }
}


// ************************************************
// * Conditions aux limites dans le cas d'hexaèdres
// ************************************************

∀ outer nodes @ 13.09 if (option_cylinder){
  node_got_tgt=false;
  node_tangent=0.0;
}


∀ outer nodes @ 13.1 if (option_cylinder){
  const Real3x3 I = matrix3x3Id();
  node_tangent=Real3(1.0,0.0,0.0);
  const Real3x3 txt = node_tangent⊗node_tangent;
  const Real3x3 P = txt;
  node_u_matrix = node_u_matrix ⊛ P;
  node_u_matrix = P ⊛ node_u_matrix;
  node_u_matrix += (I-txt)*(node_u_matrix.x.x + node_u_matrix.y.y + node_u_matrix.z.z);
  node_u_second_member = txt ⨂ node_u_second_member;
}


∀ outer nodes @ 13.3 if (option_hexa||option_cylinder){
  if (!node_x_min_max) continue;
  node_u_matrix = matrix3x3Id();
  node_u_second_member = 0.0;
}

∀ outer faces @ 13.1 if (option_cylinder){
  const Real3x3 I = matrix3x3Id();
  const Real3 Xvector=Real3(1.0,0.0,0.0);
  const Real3 normale = ½*(coord[0]-coord[1])⨯(coord[0]-coord[3]);
  ∀ node{
    const Real3 on = normale;
    if (!node_got_tgt && norm(node_tangent)==0.0){ node_tangent=on; continue;}
    // Nième passe, on regarde si on a un vecteur différent
    if (!node_got_tgt && norm(node_tangent)!=0.0){
      // S'ils ne sont pas identiques, on utilise le nouveau 'on'
      if (norm(node_tangent⨯on)!=0.0){
        node_tangent=node_tangent⨯on;
        node_tangent*=1./norm(node_tangent);
        node_tangent*=((node_tangent⋅Xvector)<0.)?-1.:1.;
        node_got_tgt=true;
        const Real3x3 txt = node_tangent⊗node_tangent;
        const Real3x3 P = txt;
        node_u_matrix = node_u_matrix ⊛ P;
        node_u_matrix = P ⊛ node_u_matrix;
        node_u_matrix += (I-txt)*(node_u_matrix.x.x + node_u_matrix.y.y + node_u_matrix.z.z );
        node_u_second_member = txt ⨂ node_u_second_member;
      }else{
        //info()<<"\t\33[33m[applyBoundaryConditionsSymmetries3DCylinder] else!\33[m";
        node_tangent=on;
      }
    }
  }
}

∀ outer faces @ 13.1 if (option_hexa){
  const Real ε=10.0*__DBL_EPSILON__;
  const Real3x3 I = matrix3x3Id();
  const Real3 center = ¼*(coord[0]+coord[1]+coord[2]+coord[3]);
  const Real3 cntryz = Real3(0.0,center.y,center.z);
  const Real3 normale = ½*(coord[0]-center)⨯(coord[3]-center);
  ∀ node{
    if (round(normale.x/ε)*ε!=0.0) continue;
    const Real orientation = ((normale⋅cntryz)<0.0)?-1.0:1.0;
    const Real3 on = orientation*normale/norm(normale);
    const Real3x3 nxn = on⊗on;
    const Real3x3 P = I-nxn;            // Matrice de projection
    node_u_matrix = node_u_matrix ⊛ P;  // ⊛ = matrixProduct
    node_u_matrix = P ⊛ node_u_matrix;  // ⊛ = matrixProduct
    node_u_matrix += nxn*(node_u_matrix.x.x + node_u_matrix.y.y + node_u_matrix.z.z );
    node_u_second_member = P ⨂ node_u_second_member; // ⨂ = prodTensVec
  }
}

